configfile: "config/config.yaml"
import os

def resolve_base(relpath: str) -> str:
  base = config.get("base_dir", None)
  if base in (None, "None", ""):
    return relpath
  return os.path.join(base, relpath)

# convenience
SAMPLES = list(config["samples"].keys())
EF = config["sae_grid"]["expansion_factor"]
TF = config["sae_grid"]["topk_fraction"]
CL = config["sae_grid"]["center_len"]
NS = config["pwm_grid"]["num_samples_per_node"]

rule all:
  input:
    expand(
      resolve_base("results/{sample}/sae/ef{ef}_tf{tf}_cl{cl}/activations/meta.json"),
      sample=SAMPLES, ef=EF, tf=TF, cl=CL
    ),
    expand(
      resolve_base("results/{sample}/sae/ef{ef}_tf{tf}_cl{cl}/pwms/ns{ns}/.done"),
      sample=SAMPLES, ef=EF, tf=TF, cl=CL, ns=NS
    )

rule train_and_collect:
  input:
    peaks      = lambda wc: resolve_base(f"resources/{wc.sample}/{config['samples'][wc.sample]['peaks_bed']}"),
    negatives  = lambda wc: resolve_base(f"resources/{wc.sample}/{config['samples'][wc.sample]['negatives_bed']}"),
    seqs       = lambda wc: resolve_base(f"resources/{wc.sample}/{config['samples'][wc.sample]['seqs']}"),
    signal_plus= lambda wc: resolve_base(f"resources/{wc.sample}/{config['samples'][wc.sample]['signal_plus']}"),
    signal_minus=lambda wc: resolve_base(f"resources/{wc.sample}/{config['samples'][wc.sample]['signal_minus']}"),
    ctl_plus   = lambda wc: resolve_base(f"resources/{wc.sample}/{config['samples'][wc.sample]['ctl_plus']}"),
    ctl_minus  = lambda wc: resolve_base(f"resources/{wc.sample}/{config['samples'][wc.sample]['ctl_minus']}"),
    model_path = lambda wc: resolve_base(f"resources/{wc.sample}/{config['samples'][wc.sample]['model_path']}")
  output:
    meta=resolve_base("results/{sample}/sae/ef{ef}_tf{tf}_cl{cl}/activations/meta.json"),
    freq_plot=resolve_base("results/{sample}/sae/ef{ef}_tf{tf}_cl{cl}/activations/node_activation_frequency_per_layer.png")
  params:
    out_dir=resolve_base("results/{sample}/sae/ef{ef}_tf{tf}_cl{cl}"),
    epochs=config["sae_grid"]["epochs"],
    inner_bs=config["sae_grid"]["inner_bs"],
    lr=config["sae_grid"]["lr"]
  conda: "envs/bpnetlite.yaml"
  resources:
    slurm_partition = "akundaje",
    gpu = 1,
    tasks_per_gpu = 0,
    runtime = "3h"
  script: "scripts/train_and_collect.py"
  
rule compute_pwms:
  input:
    activations_meta=resolve_base("results/{sample}/sae/ef{ef}_tf{tf}_cl{cl}/activations/meta.json"),
    peaks      = lambda wc: resolve_base(f"resources/{wc.sample}/{config['samples'][wc.sample]['peaks_bed']}"),
    negatives  = lambda wc: resolve_base(f"resources/{wc.sample}/{config['samples'][wc.sample]['negatives_bed']}"),
    seqs       = lambda wc: resolve_base(f"resources/{wc.sample}/{config['samples'][wc.sample]['seqs']}"),
    signal_plus= lambda wc: resolve_base(f"resources/{wc.sample}/{config['samples'][wc.sample]['signal_plus']}"),
    signal_minus=lambda wc: resolve_base(f"resources/{wc.sample}/{config['samples'][wc.sample]['signal_minus']}")
  output:
    done=resolve_base("results/{sample}/sae/ef{ef}_tf{tf}_cl{cl}/pwms/ns{ns}/.done")
  params:
    activations_dir=resolve_base("results/{sample}/sae/ef{ef}_tf{tf}_cl{cl}/activations"),
    pwm_root_dir=resolve_base("results/{sample}/sae/ef{ef}_tf{tf}_cl{cl}/pwms/ns{ns}"),
    latent_base_channels=config.get("latent_base_channels", 64)
  conda: "envs/bpnetlite.yaml"
  resources:
    slurm_partition = "normal",
    mem = "48G",
    time = "8h"
  script: "scripts/compute_pwms.py"
